apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka-android'

apply plugin: 'com.github.dcendents.android-maven'
group = rootProject.ext.group

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
//    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:0.25.0'

    implementation "com.android.support:appcompat-v7:$supportLibVersion"

    // RxJava (RxKotlin actually)
    implementation "io.reactivex.rxjava2:rxjava:2.2.0"
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation "io.reactivex.rxjava2:rxkotlin:${rootProject.ext.rxKotlinVersion}"

    // RxBinding
    // RxJava binding APIs for Android UI widgets from the platform and support libraries.
    implementation "com.jakewharton.rxbinding2:rxbinding:${rootProject.ext.rxBindingVersion}"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4:${rootProject.ext.rxBindingVersion}"
    implementation "com.jakewharton.rxbinding2:rxbinding-design:${rootProject.ext.rxBindingVersion}"

    // RxLifecycle to prevent memory leaks
    def rxLifecycleVersion = rootProject.ext.rxLifecycleVersion
    implementation "com.trello.rxlifecycle2:rxlifecycle:$rxLifecycleVersion"
    implementation "com.trello.rxlifecycle2:rxlifecycle-kotlin:$rxLifecycleVersion"
    implementation "com.trello.rxlifecycle2:rxlifecycle-android:$rxLifecycleVersion"
    implementation "com.trello.rxlifecycle2:rxlifecycle-android-lifecycle-kotlin:$rxLifecycleVersion"

    // Syntax Highlighting
    api("com.github.markusressel.KodeHighlighter:core:$kodeHighlighterVersion")

    // Zoom Layout Container
    api 'com.otaliastudios:zoomlayout:1.5.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task dokkaJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
}

artifacts {
    archives sourcesJar
    archives dokkaJar
}
